# Decode a "base64" stream

awk '/./ {
	s = s $0
	while (length(s) >= 4) {
		print substr(s, 1, 4)
		s = substr(s, 5)
	}
}' "$@" |
awk 'BEGIN {
	alph = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz"
	alph = alph "0123456789+/"
	for (i = 1; i <= length(alph); i++)
		value[substr(alph, i, 1)] = i - 1;
}
{
	v1 = value[substr($0, 1, 1)]
	v2 = value[substr($0, 2, 1)]
	v3 = value[substr($0, 3, 1)]
	v4 = value[substr($0, 4, 1)]
	o1 = v1*4 + int(v2/16)
	o2 = (v2%16)*16 + int(v3/4)
	o3 = (v3%4)*64 + v4
	printf "%c%c%c", o1, o2, o3
}' | tr -d '\015'
